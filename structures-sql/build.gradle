plugins {
	id 'org.mindignited.java-library-conventions'
	id 'io.freefair.lombok'
    id 'antlr'
}

// Configure ANTLR plugin
generateGrammarSource {
	arguments += ['-package', 'org.mindignited.structures.sql.parser']
	arguments += ['-visitor']

	// Output directly to the parser package
	outputDirectory = file("${projectDir}/src/main/java/org/mindignited/structures/sql/parser")
	enabled = project.gradle.startParameter.taskNames.contains(':structures-sql:generateGrammarSource')
}

// Configure Javadoc to exclude generated files
tasks.withType(Javadoc) {
    exclude '**/parser/StructuresSQL*.java'
    exclude '**/parser/StructuresSQL*.interp'
    exclude '**/parser/StructuresSQL*.tokens'
}

dependencies {
    // Spring dependencies
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.boot:spring-boot-autoconfigure'
    
    // Elasticsearch dependencies
    api 'co.elastic.clients:elasticsearch-java'
    api 'co.elastic.clients:elasticsearch-rest5-client'

    // Json
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api 'tools.jackson.core:jackson-core'
    api 'tools.jackson.core:jackson-databind'

    // Parsing dependencies FIXME: move versions below to dependency management
	implementation 'org.antlr:antlr4-runtime:4.13.2'
	antlr 'org.antlr:antlr4:4.13.2'

}



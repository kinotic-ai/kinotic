plugins {
    // Apply the common convention plugin for shared build configuration between library and application projects.
    id 'org.kinotic.java-common-conventions'

    id 'org.springframework.boot'
    id 'application'
    id 'io.freefair.lombok'
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation "org.kinotic:continuum-core:${continuumVersion}"
    implementation "org.kinotic:continuum-core-vertx:${continuumVersion}"

    implementation "javax.annotation:javax.annotation-api:${javaxAnnotationApi}"

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

// Options to configure here https://docs.spring.io/spring-boot/docs/2.7.18/gradle-plugin/reference/htmlsingle/#build-image
// bootBuildImage task source https://github.com/spring-projects/spring-boot/blob/main/spring-boot-project/spring-boot-tools/spring-boot-gradle-plugin/src/main/java/org/springframework/boot/gradle/tasks/bundling/BootBuildImage.java
// Paketo options here https://paketo.io/docs/howto/java/#configure-the-jvm-at-runtime
//                     https://paketo.io/docs/reference/java-reference/
bootBuildImage {
    network = "host"
    publish = System.getenv("GITHUB_ACTIONS") == "true"
    imageName = "kinoticai/${project.name}:${project.version}"
    tags = (!version.endsWith('SNAPSHOT') ?
            [
                    "kinoticai/${project.name}:latest"
            ]
            : [])
    environment = [
        "BP_HEALTH_CHECKER_ENABLED": "true",
        "THC_PATH": "/health", // Health check path for https://github.com/dmikusa/tiny-health-checker#usageor which is provided by the health-checker buildpack
        "BPE_DELIM_JAVA_TOOL_OPTIONS" : " ",
        "BPE_APPEND_JAVA_TOOL_OPTIONS": "-XX:+UseG1GC -XX:+ScavengeBeforeFullGC -XX:+DisableExplicitGC --add-opens=java.base/java.nio=ALL-UNNAMED --add-opens=java.base/java.util=ALL-UNNAMED --add-opens=java.base/java.lang.invoke=ALL-UNNAMED --add-opens=java.base/java.lang=ALL-UNNAMED",
    ]
    buildpacks = [
        "urn:cnb:builder:paketo-buildpacks/java", 
        "docker.io/paketobuildpacks/health-checker" // https://github.com/paketo-buildpacks/health-checker/tree/main
    ]
    // builder = "paketobuildpacks/ubuntu-noble-builder"
    docker {
        // Use standard Docker socket to avoid API version issues
        // host = System.getenv("DOCKER_HOST") ?: "unix:///var/run/docker.sock"
        
        publishRegistry {
            username = System.getenv("DOCKER_HUB_USERNAME")
            password = System.getenv("DOCKER_HUB_PASSWORD")
        }
    }
}


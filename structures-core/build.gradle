plugins {
	id 'org.mindignited.java-library-conventions'
	id 'io.freefair.lombok'
}

dependencies {
	implementation project(':structures-api')
	implementation project(':structures-sql')
	implementation project(':structures-auth')

	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-jackson'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-autoconfigure'
	implementation 'org.reactivestreams:reactive-streams'
	implementation 'io.projectreactor:reactor-core'

	// Spring AI
	implementation 'org.springframework.ai:spring-ai-commons'
	implementation 'org.springframework.ai:spring-ai-model'
	implementation 'org.springframework.ai:spring-ai-mcp'
	implementation 'org.springframework.ai:spring-ai-starter-model-openai'
	
	implementation ("io.vertx:vertx-web"){
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}
	implementation ("io.vertx:vertx-web-graphql"){
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}
	implementation ("io.vertx:vertx-web-client"){
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}
	implementation ("io.vertx:vertx-health-check"){
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}

	implementation ('org.apache.ignite:ignite-core'){
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}
	implementation ('org.apache.ignite:ignite-kubernetes'){
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}

	implementation "org.mindignited:continuum-core"
	implementation "org.mindignited:continuum-core-vertx"
	implementation "org.mindignited:continuum-gateway"
	implementation "org.mindignited:continuum-idl"

	implementation "com.github.ben-manes.caffeine:caffeine"

	implementation "com.graphql-java:graphql-java"
	implementation "com.apollographql.federation:federation-graphql-java-support:${graphQlFederationJvmVersion}"
	implementation "com.graphql-java:graphql-java-extended-scalars:${graphQlJavaExtendedScalarsVersion}"

	// Json
	api 'com.fasterxml.jackson.core:jackson-annotations'
	api 'tools.jackson.core:jackson-core'
	api 'tools.jackson.core:jackson-databind'

	implementation "io.swagger.core.v3:swagger-models:${swaggerCoreVersion}"
	implementation "io.swagger.core.v3:swagger-core:${swaggerCoreVersion}"

	implementation "com.github.slugify:slugify:${slugifyVersion}"

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
	testImplementation "org.testcontainers:elasticsearch:${testContainersVersion}"
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.security:spring-security-oauth2-client'
}

// Configure Javadoc to exclude non-Java files
tasks.named('javadoc') {
	exclude '**/*.interp', '**/*.tokens' // Exclude ANTLR artifacts
}

test {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
	environment 'JAVA_TOOL_OPTIONS', '-Djava.net.preferIPv4Stack=true --add-opens=jdk.management/com.sun.management.internal=ALL-UNNAMED --add-opens=java.base/jdk.internal.misc=ALL-UNNAMED --add-opens=java.base/sun.nio.ch=ALL-UNNAMED --add-opens=java.management/com.sun.jmx.mbeanserver=ALL-UNNAMED --add-opens=java.base/sun.reflect.generics.reflectiveObjects=ALL-UNNAMED --add-opens=java.base/java.io=ALL-UNNAMED --add-opens=java.base/java.nio=ALL-UNNAMED --add-opens=java.base/java.util=ALL-UNNAMED --add-opens=java.base/java.lang=ALL-UNNAMED --add-opens=java.base/java.lang.invoke=ALL-UNNAMED'
    
    // Exclude cluster tests by default (they are resource-intensive and slow)
    // Cluster tests are annotated with @Disabled and can be run explicitly:
    // ./gradlew :structures-core:test --tests ClusterCacheEvictionTest
    exclude '**/cluster/**'
}

// Task to run only cluster tests (requires Docker)
// Usage: ./gradlew :structures-core:clusterTest
// Prerequisites: 
//   1. Docker running
//   2. Server image built: ./gradlew :structures-server:bootBuildImage
task clusterTest(type: Test) {
    description = 'Runs cluster integration tests using Testcontainers (requires Docker)'
    group = 'verification'
    
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
	environment 'JAVA_TOOL_OPTIONS', '-Djava.net.preferIPv4Stack=true --add-opens=jdk.management/com.sun.management.internal=ALL-UNNAMED --add-opens=java.base/jdk.internal.misc=ALL-UNNAMED --add-opens=java.base/sun.nio.ch=ALL-UNNAMED --add-opens=java.management/com.sun.jmx.mbeanserver=ALL-UNNAMED --add-opens=jdk.internal.jvmstat/sun.jvmstat.monitor=ALL-UNNAMED --add-opens=java.base/sun.reflect.generics.reflectiveObjects=ALL-UNNAMED --add-opens=java.base/java.io=ALL-UNNAMED --add-opens=java.base/java.nio=ALL-UNNAMED --add-opens=java.base/java.util=ALL-UNNAMED --add-opens=java.base/java.lang=ALL-UNNAMED --add-opens=java.base/java.lang.reflect=ALL-UNNAMED --add-opens=java.base/java.lang.invoke=ALL-UNNAMED --add-opens=java.base/java.lang.in=ALL-UNNAMED'
    // systemProperty "STRUCTURES_TEST_USE_EXTERNAL_ELASTICSEARCH", "true"

    // Only include cluster tests
    include '**/cluster/**'
    
    // These tests take longer
    timeout = Duration.ofMinutes(10)
    
    shouldRunAfter test
    
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}
